
/*
 * Copyright (c) 2015 Luca Burgazzoli
 *
 * https://github.com/lburgazzoli
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// *****************************************************************************
// -Djava.util.logging.manager=org.apache.logging.log4j.jul.LogManager
// *****************************************************************************

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()

        //maven { url 'http://repo.spring.io/libs-release' }
        //maven { url 'http://repo.spring.io/libs-milestone' }
        //maven { url 'http://repo.spring.io/libs-snapshot' }
    }

    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.8'
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.2.3.RELEASE'
        classpath 'com.ofg:uptodate-gradle-plugin:1.5.1'
    }
}

// *****************************************************************************
//
// *****************************************************************************

subprojects {
    
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'maven'
    apply plugin: 'idea'
    apply plugin: 'osgi'
    apply plugin: 'com.github.ben-manes.versions'
    apply plugin: 'com.ofg.uptodate'
    apply plugin: 'com.bmuschko.nexus'


    version             = '1.0.0.SNAPSHOT'
    group               = 'com.github.lburgazzoli'
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    
    project.ext {
        gradleVersion       = '2.3'
        gradleScriptDir     = "${rootProject.projectDir}/gradle"
        isReleaseVersion    = !version.endsWith("SNAPSHOT")
        gitRoot             = "https://github.com/lburgazzoli"
        gitProject          = "https://github.com/lburgazzoli/${project.name}"
        deploySnapshot      = "https://oss.sonatype.org/content/repositories/snapshots"
        gradleScriptDir     = "${rootProject.projectDir}/gradle"
        osgiVersion         = project.version.replaceAll('-','.')

        if(project.hasProperty('sonatypeUsername') && project.hasProperty('sonatypePassword')) {
            nexusUsername = sonatypeUsername
            nexusPassword = sonatypePassword
        } else {
            nexusUsername = "$System.env.CI_DEPLOY_USERNAME"
            nexusPassword = "$System.env.CI_DEPLOY_PASSWORD"
        }

        versions = [
            gradle             : '2.3',
            slf4j              : '1.7.12',
            log4j2             : '2.2',
            jackson            : '2.5.2',
            jacksonCore        : '2.5.2',
            jacksonDatabind    : '2.5.2',
            jacksonSmile       : '2.5.2',
            guava              : '18.0',
            commonsLang        : '3.4',
            commonsCollections : '4.0',
            commonsPool        : '2.3',
            commonsIO          : '2.4',
            commonsCLI         : '1.2',
            commonsLogging     : '1.2',
            commonsCodec       : '1.10',
            commonsMath        : '3.4.1',
            spring             : '4.1.6.RELEASE',
            springSecurity     : '4.0.0.RELEASE',
            springBoot         : '1.2.3.RELEASE',
            hikariCp           : '2.3.6',
            hsqldb             : '2.3.2',
            h2                 : '1.4.187',
            gsCollections      : '6.1.0',
            groovy             : '2.4.3',
            jolokia            : '1.2.3',
            javaxInject        : '1',
            javaxEl            : '3.0.0',
            hibernateValidator : '5.1.3.Final',
            junit              : '4.12'
        ]
    }
    
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        compile("org.slf4j:slf4j-api:$versions.slf4j") { transitive = false }
        compile("org.slf4j:slf4j-ext:$versions.slf4j") { transitive = false }

        runtime "org.apache.logging.log4j:log4j-api:$versions.log4j2"
        runtime "org.apache.logging.log4j:log4j-core:$versions.log4j2"
        runtime "org.apache.logging.log4j:log4j-jcl:$versions.log4j2"
        runtime "org.apache.logging.log4j:log4j-jul:$versions.log4j2"
        runtime "org.apache.logging.log4j:log4j-slf4j-impl:$versions.log4j2"

        compile "com.google.guava:guava:$versions.guava"
        compile "com.goldmansachs:gs-collections:$versions.gsCollections"

        compile "commons-codec:commons-codec:$versions.commonsCodec"
        compile "commons-io:commons-io:$versions.commonsIO"
        compile "commons-cli:commons-cli:$versions.commonsCLI"
        compile "commons-logging:commons-logging:$versions.commonsLogging"
        compile "org.apache.commons:commons-pool2:$versions.commonsPool"
        compile "org.apache.commons:commons-lang3:$versions.commonsLang"
        compile "org.apache.commons:commons-collections4:$versions.commonsCollections"
        compile "org.apache.commons:commons-math3:$versions.commonsMath"
        
        compile("org.springframework:spring-core:$versions.spring") {
            exclude group:'commons-logging', module:'commons-logging'
        }
        compile("org.springframework:spring-context:$versions.spring") {
            exclude group:'commons-logging', module:'commons-logging'
        }
        compile("org.springframework:spring-web:$versions.spring") {
            exclude group:'commons-logging', module:'commons-logging'
        }
        compile("org.springframework.security:spring-security-core:$versions.springSecurity") {
            exclude group:'commons-logging', module:'commons-logging'
        }

        compile "com.fasterxml.jackson.core:jackson-core:$versions.jacksonCore"
        compile "com.fasterxml.jackson.core:jackson-databind:$versions.jacksonDatabind"
        compile "com.fasterxml.jackson.core:jackson-annotations:$versions.jackson"
        compile "com.fasterxml.jackson.module:jackson-module-afterburner:$versions.jackson"
        compile "com.fasterxml.jackson.dataformat:jackson-dataformat-smile:$versions.jacksonSmile"
        compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$versions.jackson"

        compile "org.codehaus.groovy:groovy-all:$versions.groovy:indy"
        compile "com.zaxxer:HikariCP:$versions.hikariCp"

        compile "com.h2database:h2:$versions.h2"
        
        compile "javax.inject:javax.inject:$versions.javaxInject"
        compile "javax.el:javax.el-api:$versions.javaxEl"
        compile "org.hibernate:hibernate-validator:$versions.hibernateValidator"

        testCompile "junit:junit:$versions.junit"
    }

    jar {
        baseName = "${project.name}"

        manifest.attributes['Bundle-Description'    ] = "lb :: ${project.name}"
        manifest.attributes['Bundle-Name'           ] = "lb :: ${project.name}"
        manifest.attributes['Bundle-SymbolicName'   ] = "${project.group}-${project.name}"
        manifest.attributes['Bundle-Version'        ] = project.version
        manifest.attributes['Implementation-Title'  ] = "${project.group}-${project.name}-${project.version}"
        manifest.attributes['Implementation-Version'] = project.version
        manifest.attributes['Implementation-Vendor' ] = 'lb'
    }

    test {
        testLogging {
            events          'failed'
            exceptionFormat 'full'
        }
    }

    [compileJava, compileTestJava]*.options*.compilerArgs = [
        "-Xlint:unchecked",
        "-Xlint:deprecation",
    ]

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }

    dependencyUpdates {
        revision = 'release'
    }

    uptodate {
        reportProjectName true
        addExcludedVersionPatterns '.*does-not-exist.*'
        addExcludedVersionPatterns '.*b\\d+$'
        addExcludedVersionPatterns '^20\\d\\d\\d\\d\\d\\d.*$'
    }

    tasks.withType(GroovyCompile) {
        groovyOptions.optimizationOptions.indy = true
    }

    // *************************************************************************
    //
    // *************************************************************************

    nexus {
        sign    = false
    }

    extraArchive {
        javadoc = true
        sources = true
        tests   = false
    }

    modifyPom {
        project {
            name          = "${project.name}"
            description   = "${project.name}"
            url           = "${project.gitProject}"
            packaging     = 'jar'

            licenses {
                license {
                    name         = "The Apache Software License, Version 2.0"
                    url          = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    distribution = "repo"
                }
            }

            scm {
                url                 = "${project.gitProject}"
                connection          = "scm:git:${project.gitProject}.git"
                developerConnection = "scm:git:${project.gitProject}.git"
            }
        }
    }
}

// *****************************************************************************
//
// *****************************************************************************

task wrapper(type: Wrapper) {
    gradleVersion = "$gradleVersion"
}
